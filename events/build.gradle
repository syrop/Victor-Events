apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion '28.0-rc2'

    defaultConfig {
        applicationId 'pl.org.seva.events'
        minSdkVersion 26
        targetSdkVersion 27
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            crunchPngs true
        }
    }

    /* Android Extensions plugin includes several experimental
       features such as LayoutContainer support and a Parcelable
       implementation generator. These features are not considered
       production ready yet, so you need to turn on the experimental
       mode in build.gradle in order to use them:

       https://kotlinlang.org/docs/tutorials/android-plugin.html
     */

    androidExtensions {
        experimental = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    kotlin { experimental { coroutines 'enable' } }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // https://androidstudio.googleblog.com/2018/04/android-studio-32-canary-11-now.html
    lintOptions {
        check 'Interoperability'
    }
}

kapt {
    // https://blog.jetbrains.com/kotlin/2018/01/kotlin-1-2-20-is-out/
    useBuildCache = true
    // https://blog.jetbrains.com/kotlin/2018/03/kotlin-1-2-30-is-out/
    mapDiagnosticLocations = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Support
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.20'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    implementation 'org.kodein.di:kodein-di-generic-jvm:5.0.0'
    implementation 'org.kodein.di:kodein-di-framework-android:5.0.0'
    implementation 'org.kodein.di:kodein-di-conf-jvm:5.0.0'

    // Play, Firebase
    implementation 'com.google.android.gms:play-services-auth:15.0.1'
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-auth:16.0.2'
    implementation 'com.google.firebase:firebase-firestore:17.0.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'

    // Architecture
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    kapt 'android.arch.persistence.room:compiler:1.1.1'
    testImplementation 'android.arch.persistence.room:testing:1.1.1'
    implementation 'android.arch.persistence.room:rxjava2:1.1.1'

    implementation 'commons-io:commons-io:2.5'
    androidTestImplementation 'org.mockito:mockito-android:2.18.3'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
